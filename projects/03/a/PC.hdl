// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=registerOut, out=incOut);

    // Calculate load
    Or(a=reset, b=load, out=orOut1);
    Or(a=orOut1, b=inc, out=loadOut);

    // Calculate value that should be propagated as in
    Mux16(a=registerOut, b=incOut, sel=inc, out=temp1);
    Mux16(a=temp1, b=in, sel=load, out=temp2);
    Mux16(a=temp2, b=false, sel=reset, out=temp3);

    // Feed the final register
    Register(in=temp3, load=loadOut, out=out, out=registerOut);
}
